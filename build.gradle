plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '2.7.7'
    id 'net.ltgt.errorprone' version '2.0.1'
}

group = 'ro.carol.application'
version = '1.0-SNAPSHOT'

apply plugin : 'io.spring.dependency-management'

repositories {
    mavenCentral()
}

springBoot {
    buildInfo()
}

java{
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.errorprone {
            disableWarningsInGeneratedCode = true
            excludedPaths = ".*/build/generated/.*"
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.google.code.findbugs:jsr305'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.postgresql:postgresql'
    implementation 'org.liquibase:liquibase-core'

    errorprone 'com.google.errorprone:error_prone_core:2.18.0'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.springframework.restdocs:spring-restdocs-core'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = file("$buildDir/jacoco")
}